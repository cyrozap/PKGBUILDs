# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=hamlib
pkgver=1.2.15.3
pkgrel=1
pkgdesc="HAM radio equipment control libraries"
arch=('i686' 'x86_64')
url="http://hamlib.org"
license=('GPL' 'LGPL')
depends=('perl' 'python2' 'tcl' 'libxml2' 'libusb')
makedepends=('libtool' 'swig')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz \
			 fix-configure.patch)
options=('!emptydirs')
changelog=${pkgname}.ChangeLog.markdown

# Fix this bug, that prevents building 1.2.15.3
# https://github.com/N0NB/hamlib/commit/302d0b2ffe4e8dd49556818e0abb244c1ef6d86a
prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -N -p1 < $srcdir/fix-configure.patch
}

build() {
	export PYTHON=/usr/bin/python2

	cd $srcdir

	cd $pkgname-$pkgver
	

	./configure --prefix=/usr \
		--with-perl-binding \
		--with-python-binding \
		--enable-tcl-binding

	make
}

package() {
	cd $srcdir/$pkgname-$pkgver

	make DESTDIR=$pkgdir install

	# fix perl module location
	cd $pkgdir/usr/lib/perl5/site_perl/
	mkdir current/
	mv auto current
	mv Hamlib.pm current
	rm perltest.pl

	/usr/bin/find $pkgdir -name '.packlist' -delete
	/usr/bin/find $pkgdir -name '*.pod' -delete
}

md5sums=('3cad8987e995a00e5e9d360e2be0eb43'
         '043a6b399c68aa2db899cac8a4ba334e')
sha256sums=('a2ca4549e4fd99d6e5600e354ebcb57502611aa63c6921c1b8a825289833f75e'
            'f56546785ae1815a00b61525b257e0f325b0745969b4f2abb569941b91261315')
